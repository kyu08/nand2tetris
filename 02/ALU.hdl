// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    // pre_process - zx
    Mux16(a=x, b[0..15]=false, sel=zx, out=zxResult);

    // pre_process - nx
    Not16(in=zxResult, out=notX);
    Mux16(a=zxResult, b=notX, sel=nx, out=nxResult);

    // pre_process - zy
    Mux16(a=y, b[0..15]=false, sel=zy, out=zyResult);

    // pre_process - ny
    Not16(in=zyResult, out=notY);
    Mux16(a=zyResult, b=notY, sel=ny, out=nyResult);

    // calculate
    // if f == 1 then: x+y: Add16
    // else x&y: And16
    And16(a=nxResult, b=nyResult, out=and);
    Add16(a=nxResult, b=nyResult, out=add);
    Mux16(a=and, b=add, sel=f, out=calculateResult);

    // post_process
    Not16(in=calculateResult, out=notCalculateResult);
    // NOTE: 出力の一部を呼び出したい場合は、out[0..7]=out0To7 のように指定しておく必要がある
    Mux16(a=calculateResult, b=notCalculateResult, sel=no, out=out, out[0..7]=out0To7, out[8..15]=out8To15, out[15]=ng);

    // zr
    Or8Way(in=out0To7, out=zrUpTo7);
    Or8Way(in=out8To15, out=zrUpTo15);
    Or(a=zrUpTo7, b=zrUpTo15, out=notZr);
    Not(in=notZr, out=zr);
}
